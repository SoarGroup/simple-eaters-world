rl -s learning on
indiff -g
indiff -e .1

# Initialization operator
sp {propose*initialize-eater
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name eater)
}

sp {apply*initialize-eater
   (state <s> ^operator <op>)
   (<op> ^name eater)
-->
   (<s> ^name eater
        ^direction north south east west)
}

# cleans the output-link once commands complete
sp {apply*cleanup*output-link
   (state <s> ^operator <op>
              ^superstate nil
              ^io.output-link <out>)
   (<out> ^<cmd> <id>)
   (<id> ^status)
-->
   (<out> ^<cmd> <id> -)
}

# misc useful elaboration rules
sp {elaborate*state*name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

sp {elaborate*state*top-state
   (state <s> ^superstate.top-state <ts>)
-->
   (<s> ^top-state <ts>)
}

sp {elaborate*top-state*top-state
   (state <s> ^superstate nil)
-->
   (<s> ^top-state <s>)
}

sp {elaborate*state*io
   (state <s> ^superstate.io <io>)
-->
   (<s> ^io <io>)
}

####

sp {end
   (state <s> ^io.input-link.food-remaining 0)
-->
   (halt)
}

sp {propose*move
   (state <s> ^name eater
              ^direction <d>
              ^io.input-link.<d> <contents>)
-->
   (<s> ^operator <op> +)
   (<op> ^name move
         ^direction <d>
         ^contents <contents>)
}

sp {propose*move*rotate
   (state <s> ^name move
              ^superstate <ss>)
   (<ss> ^operator <ss-op>
         ^io.input-link.orientation <dir>)
   (<ss-op> ^direction <> <dir>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name rotate)
}

sp {apply*rotate
   (<s> ^operator.name rotate
        ^superstate.io.output-link <out>)
  -(<out> ^rotate)
-->
   (<out> ^rotate <r>)
}

sp {propose*move*forward
   (state <s> ^name move
              ^superstate <ss>)
   (<ss> ^operator <ss-op>
         ^io.input-link.orientation <dir>)
   (<ss-op> ^direction <dir>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name forward)
}

sp {apply*forward
   (<s> ^operator.name forward
        ^superstate.io.output-link <out>)
  -(<out> ^forward)
-->
   (<out> ^forward <r>)
}

# Learn value of movement
gp {eater*evaluate*move
      (state <s> ^name eater
                 ^operator <op> +)
      (<op> ^name move
            ^contents [red wall blue empty green purple])
   -->
      (<s> ^operator <op> = 0.0)
}

# Provide reward
sp {eater*elaborate*state   (state <s> ^name eater              ^reward-link <rl>              ^io.input-link.score-diff <d>)-->   (<rl> ^reward.value <d>)}
